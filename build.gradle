plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0-RC1'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.17.0'
}

group = 'com.ticket'
version = '0.0.1-SNAPSHOT'
description = 'Cinimas online booking  '

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

openApiGenerate {
    inputSpec = "${rootDir}/src/main/resources/openapi.yaml"
    generatorName = "spring"
    library = "spring-boot"
    outputDir = "${layout.buildDirectory.get().asFile}/generated"
    apiPackage = "com.ticket.transaction.api"
    modelPackage = "com.ticket.transaction.model"
    configOptions = [
            interfaceOnly: "true",
            dateLibrary: "java8",
            useJakartaEe: "true",
            openApiNullable: "false",
            useBeanValidation: "true",
            useSpringBoot3: "true",
            useTags: "true",
            useLombok: "true",
            additionalModelTypeAnnotations: "@lombok.Builder"
    ]

}

// fix Mockito's dynamic agent loading in Java 21
tasks.named('test') {
    useJUnitPlatform()
    jvmArgs '-XX:+EnableDynamicAgentLoading'
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "${layout.buildDirectory.get().asFile}/generated/src/main/java"]
        }
    }
}

tasks.named('compileJava') {
    dependsOn tasks.named('openApiGenerate')
    options.compilerArgs += ['-Xlint:none', '-Xlint:-processing']
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'jakarta.servlet:jakarta.servlet-api'
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.22'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-actuator-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-hateoas-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}
