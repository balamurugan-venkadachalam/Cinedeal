plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.17.0'
}

group = 'com.cinema.pricing'
version = '0.0.1-SNAPSHOT'
description = 'Movie ticket price calculator'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

openApiGenerate {
    inputSpec = "${rootDir}/src/main/resources/openapi.yaml"
    generatorName = "spring"
    library = "spring-boot"
    outputDir = "${layout.buildDirectory.get().asFile}/generated"
    apiPackage = "com.cinema.pricing.api"
    modelPackage = "com.cinema.pricing.model"
    configOptions = [
            interfaceOnly: "true",
            dateLibrary: "java8",
            useJakartaEe: "true",
            openApiNullable: "false",
            useBeanValidation: "true",
            useSpringBoot3: "true",
            useTags: "true",
            useLombok: "true",
            sortParamsByRequiredFlag: "false",
            sortModelPropertiesByRequiredFlag: "false",
            additionalModelTypeAnnotations: "@lombok.Builder @lombok.ToString"
    ]

}

// fix Mockito's dynamic agent loading in Java 21
tasks.named('test') {
    useJUnitPlatform()
    jvmArgs '-XX:+EnableDynamicAgentLoading'
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "${layout.buildDirectory.get().asFile}/generated/src/main/java"]
        }
    }
}

tasks.named('compileJava') {
    dependsOn tasks.named('openApiGenerate')
    options.compilerArgs += ['-Xlint:none', '-Xlint:-processing']
}

repositories {
	mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Explicitly exclude vulnerable commons-lang3 from springdoc and add patched version
    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0') {
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }
    // Explicitly add the patched version
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'jakarta.servlet:jakarta.servlet-api'
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.22'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}
